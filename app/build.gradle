apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.patloew.countries"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/armeabi/librealm-jni.so'
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_6
}

ext.supportLibVersion = '24.1.1'
ext.retrofitVersion = '2.1.0'
ext.okHttpVersion = '3.4.1'
ext.daggerVersion = '2.6'
ext.parcelerVersion = '1.1.5'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    compile 'com.google.code.gson:gson:2.7'

    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.6'

    compile 'com.jakewharton.timber:timber:4.1.2'

    compile "org.parceler:parceler-api:${parcelerVersion}"
    apt "org.parceler:parceler:${parcelerVersion}"

    apt 'com.google.guava:guava:19.0' // needed by Dagger
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"

    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile('com.mikepenz:aboutlibraries:5.6.2@aar') {
        transitive = true
    }

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.powermock:powermock-module-junit4:1.6.5"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.5"
    testCompile "org.powermock:powermock-api-mockito:1.6.5"
}
