apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

def gitCommitHash() {
    def commitHash = ""
    def proc = "git rev-parse --short HEAD".execute()
    proc.in.eachLine { line -> commitHash = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    commitHash
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    signingConfigs {
        release {
            keyAlias 'mykeyalias'
            keyPassword 'mykeypassword'
            storeFile file('../keystore.jks')
            storePassword 'mykeystorepassword'
        }
    }
    defaultConfig {
        applicationId "com.tailoredapps.template"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        setProperty("archivesBaseName", "TAAppTemplate-v$versionName-b$versionCode-" + gitCommitHash())
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    productFlavors {
        devBackend {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "TA Template App Dev"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
        }
        productionBackend {
            resValue "string", "app_name", "TA Template App"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_6
}

ext.supportLibVersion = '25.1.0'
ext.retrofitVersion = '2.1.0'
ext.okHttpVersion = '3.5.0'
ext.daggerVersion = '2.8'
ext.parcelerVersion = '1.1.6'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-fragment:$supportLibVersion"
    compile "com.android.support:support-core-ui:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    compile 'com.google.code.gson:gson:2.8.0'

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    //compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.11'

    compile 'com.jakewharton.timber:timber:4.5.0'

    compile "org.parceler:parceler-api:$parcelerVersion"
    apt "org.parceler:parceler:$parcelerVersion"

    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"

    compile 'io.reactivex.rxjava2:rxjava:2.0.4'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile('com.mikepenz:aboutlibraries:5.8.1@aar') {
        transitive = true
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.4.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.powermock:powermock-module-junit4:1.6.5"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.5"
    testCompile "org.powermock:powermock-api-mockito:1.6.5"
    testCompile 'io.reactivex:rxjava:1.2.3'

    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    androidTestCompile 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
}
